s=17 m=1000 ib=5 t=20 g=0.200 p=0 i=0 x=ib+s u=0
if :APRCH*(:AU<m) then goto4-:collisionDamage end goto1
Kp=0.056 Ki=0.001 Kd=5.4 h=0.50 l=1-h p=:AU-s goto5 //NSZ Conservative
Kp=0.080 Ki=0.001 Kd=5.8 h=0.50 l=1-h p=:AU-s goto5 //SZ  Aggressive
e=:AU-s d=(p-e)*Kd*h+d*l i+=e*Ki*(:AU<x) o=Kp*e+i f=u/t u+=u<t p=e
z/=:APRCH*(:AU<m)*(d*d>g+e*e>1) :FWD=o*f :BWD=(d-i)*f goto5
if (d*d>g+e*e>1)==0 then :APRCH=0 end :FWD=0 :BWD=0 goto1

//Improved PID - Approximated Profiled Motion PID - VER 1.1
// by: Darkyshadow built and tuned for Ghidorah of HLA
// github.com/GameName-Darkyshadow/Starbase
// tips by in game mail are not necessary but appreciated
// Tuning Parameters explained on Github with Manual

//Large Ship Example Tunings
//Kp=0.056 Ki=0.001 Kd=5.4 h=0.50 //t=20 //Super Conservative
//Kp=0.080 Ki=0.001 Kd=5.8 h=0.50 //t=20 //Conservative
//Medium Ship Example Tunings
//Kp=0.090 Ki=0.001 Kd=5.8 h=0.98 //t=10  //Medium Aggressive
